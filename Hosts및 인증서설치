@echo off
chcp 65001 >nul

:: 관리자 권한 확인
>nul 2>&1 "%SYSTEMROOT%\system32\cacls.exe" "%SYSTEMROOT%\system32\config\system"

if '%errorlevel%' NEQ '0' (
    echo 관리자 권한이 필요합니다. UAC 권한 요청 중...
    goto UACPrompt
) else (
    goto GotAdmin
)

:UACPrompt
    echo Set UAC = CreateObject^("Shell.Application"^) > "%temp%\getadmin.vbs"
    set "params=%*"
    echo UAC.ShellExecute "cmd.exe", "/c ""%~s0"" %params%", "", "runas", 1 >> "%temp%\getadmin.vbs"

    "%temp%\getadmin.vbs"
    del "%temp%\getadmin.vbs"
    exit /B

:GotAdmin
    pushd "%CD%"

    echo.>> "%SystemRoot%\System32\drivers\etc\hosts"
    echo 192.168.10.10 vdi.test.com>> "%SystemRoot%\System32\drivers\etc\hosts"
    echo [INFO] hosts 파일에 도메인 매핑 추가 완료

    certutil -addstore "Root" "C:\test\test_RootCA.crt"
    echo [INFO] 루트 인증서 설치 완료

    rmdir /s /q "C:\test"
    echo [INFO] C:\test 디렉토리 삭제 완료

    popd
    echo [완료] 모든 작업이 정상적으로 수행되었습니다.
    pause
